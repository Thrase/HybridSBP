# RUN WITH CUBIT 15.2

# Set units to SI.
${Units('si')}

#---------------
reset

${Lx=100}
${H=15}
${h=12}
${W=40}
${Ly=100}

${d0=0}
${d4=0}

${a=12}

#  10--1--5--2-11
#  |   |  |  |  |
#  |   |  6  |  |
#  |   |  |  |  |
#  |   |  7  |  |
#  |   |  |  |  |
#  |   3--8--4  |
#  |      |     |
#  12-----9----13
create vertex    {-W}        0  0 #v1
create vertex     {W}        0  0 #v2
create vertex    {-W}     {-W}  0 #v3
create vertex     {W}     {-W}  0 #v4
create vertex       0        0  0 #v5
create vertex       0     {-H}  0 #v6
create vertex       0   {-H-h}  0 #v7
create vertex       0     {-W}  0 #v8
create vertex       0    {-Ly}  0 #v9
create vertex    {-Lx}       0  0 #v10
create vertex    {+Lx}       0  0 #v11
create vertex    {-Lx}   {-Ly}  0 #v12
create vertex    {+Lx}   {-Ly}  0 #v13


# Create Outer box
create curve vertex  10 11 #c1
create curve vertex  10 12 #c2
create curve vertex  12 13 #c3
create curve vertex  13 11 #c4
create surface curve 1 2 3 4
delete curve 1 2 3 4

# Create Fault Box
create curve vertex  3  4 #c5
create curve vertex  1  3 #c6
create curve vertex  2  4 #c7
imprint tolerant surface 1 with curve 5 6 7 merge
delete curve 5 6 7

Create Fault
create curve vertex  5  6 #c14
create curve vertex  6  7 #c15
create curve vertex  7  8 #c16
create curve vertex  8  9 #c17

imprint tolerant surface 2 with curve 14 15 16 merge
delete curve 14 15 16

imprint tolerant surface 3 with curve 17 merge
delete curve 17

surface 6 7 size {W}
mesh surface 6 7

refine curve 8 22 23 10 numsplit 1 bias 1.0 depth 0 smooth

surface 4 5 size {a}
mesh surface 4 5

refine curve 18 19 20 numsplit 2 bias 1.0 depth 1 smooth

Sideset 1 add curve 2 4               # Displacement (half V_p)
Sideset 2 add curve 26 27 12 13 21 24 # Free
Sideset 3 add curve 25                # Displacement jump with V_p
Sideset 7 add curve 18 19 20          # Frictional fault

export Abaqus "/Users/jekozdon/codes/D2_sbp/meshes/BP1_V1.inp"  overwrite
